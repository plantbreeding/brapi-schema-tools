plugins {
    id 'brapi.schema-tools.java-library-conventions'
}

dependencies {
    api 'com.graphql-java:graphql-java:22.0'
    api 'io.swagger.core.v3:swagger-core:2.2.22'
    api 'org.apache.jena:jena-ontapi:5.1.0'

    implementation 'com.networknt:json-schema-validator:1.0.72'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.1'
    implementation 'io.swagger.parser.v3:swagger-parser:2.1.22'
    implementation 'org.apache.poi:poi:5.2.5'
    implementation 'org.apache.poi:poi-ooxml:5.2.5'
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'org.atteo:evo-inflector:1.3'
    implementation 'org.graalvm.js:js:24.1.2'
    implementation 'org.jruby.joni:joni:2.2.5'
    implementation 'org.openapitools.openapidiff:openapi-diff-core:2.1.0'
    implementation 'com.github.vertical-blank:sql-formatter:2.0.5'
    implementation 'org.dflib:dflib:2.0.0-M6'
    implementation 'org.dflib:dflib-json:2.0.0-M6'
}

mavenPublishing {
    coordinates("org.brapi.brapi-schema-tools", "brapi-schema-tools-core", project.version)
}

tasks.register('generateVersionFile') {
    def outputFile = file("$buildDir/generated-resources/version.txt")
    outputs.file(outputFile)
    doLast {
        def version = project.version?.toString()
        println "Generating version.txt with version: ${version}"
        if (!version || version == 'unspecified') {
            throw new GradleException("Project version is not set. Cannot generate version.txt.")
        }
        outputFile.parentFile.mkdirs()
        outputFile.text = version
    }
}

processResources {
    from("$buildDir/generated-resources") {
        include 'version.txt'
    }
    dependsOn tasks.named('generateVersionFile')
}

